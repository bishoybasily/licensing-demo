load("@aspect_bazel_lib//lib:tar.bzl", "tar")
load("@container_structure_test//:defs.bzl", "container_structure_test")
load("@rules_java//java:defs.bzl", "java_binary", "java_library", "java_plugin")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push")

package(default_visibility = ["//visibility:public"])

java_plugin(
    name = "lombok_plugin",
    processor_class = "lombok.launch.AnnotationProcessorHider$AnnotationProcessor",
    deps = [
        "@maven//:org_projectlombok_lombok",
    ],
)

java_binary(
    name = "application_binary",
    srcs = glob(["**/*.java"]),
    javacopts = [
        "-source",
        "21",
        "-target",
        "21",
    ],
    main_class = "com.gmail.bishoybasily.licensing.application.Main",
    plugins = [":lombok_plugin"],
    resource_strip_prefix = "src/java/main/application/resources",
    resources = glob(["resources/**"]),
    deps = [
        "//src/java/main/commons:commons_library",
        "//src/java/main/enforcer:enforcer_library",

        # kubernetes client
        "@maven//:io_kubernetes_client_java",
        "@maven//:io_kubernetes_client_java_api",

        # reactor
        "@maven//:io_projectreactor_reactor_core",

        # lombok
        "@maven//:org_projectlombok_lombok",

        # spring
        "@maven//:org_springframework_boot_spring_boot",
        "@maven//:org_springframework_boot_spring_boot_autoconfigure",
        "@maven//:org_springframework_boot_spring_boot_starter_webflux",
        "@maven//:org_springframework_spring_context",
        "@maven//:org_springframework_spring_web",
    ],
)

tar(
    name = "layer",
    srcs = ["application_binary_deploy.jar"],
)

oci_image(
    name = "application_binary_image",
    base = "@java_base",
    entrypoint = [
        "java",
        "-jar",
        "/java-maven-deploy.jar",
    ],
    tars = [":layer"],
)

oci_push(
    name = "application_binary_image_push",
    image = ":application_binary_image",
    remote_tags = [
        "latest",
        "1.0.0",
    ],
    repository = "ghcr.io/bishoybasily/licensing-demo-application",
)
